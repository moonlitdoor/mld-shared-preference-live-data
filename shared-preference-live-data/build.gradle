apply plugin: "com.moonlitdoor.git-version"
apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply plugin: "org.jetbrains.dokka"

project.ext.archivesBaseName = project.name + "-" + gitVersion

android {
  compileSdkVersion 28
  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 28
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    warningsAsErrors true
    abortOnError true
  }
  testOptions {
    execution "ANDROID_TEST_ORCHESTRATOR"
  }
}

tasks.matching {it instanceof Test}.all {
  testLogging.events = ["failed", "passed", "skipped"]
}

repositories {
  maven {url "https://oss.sonatype.org/content/repositories/snapshots"}
}

dependencies {

  implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.71"

  testImplementation "junit:junit:4.12"
  testImplementation "com.android.support.test.espresso:espresso-core:3.1.0-beta01"
  testImplementation "org.robolectric:robolectric:4.0-alpha-4-SNAPSHOT"

  androidTestImplementation "androidx.test:runner:1.1.0-beta01"
  androidTestImplementation "androidx.test:rules:1.1.0-beta01"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0-beta01"
}

dokka {
  outputFormat = "html"
  outputDirectory = "${project.projectDir}/build/javadoc"
}

task sourcesAar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "sources"
  version = gitVersion
  extension = "aar"
}

task javadocAar(type: Jar) {
  dependsOn dokka
  from "${project.projectDir}/build/javadoc"
  classifier "javadoc"
  version = gitVersion
}

publishing {
  publications {
    library(MavenPublication) {
      artifactId = project.name
      version = gitVersion
      artifact "build/outputs/aar/" + archivesBaseName + "-release.aar"
      artifact "build/publications/library/pom-default.xml"
      pom {
        name = "SharedPreferenceLiveData"
        description = "An Android library"
        url = "http://github.com/moonlitdoor/mld-shared-preference-live-data"
        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }
        scm {
          connection = "scm:git:git://github.com/moonlitdoor/mld-shared-preference-live-data.git"
          developerConnection = "scm:git:ssh://github.com/moonlitdoor/mld-shared-preference-live-data.git"
          url = "http://github.com/moonlitdoor/mld-shared-preference-live-data"
        }
      }
    }
    sources(MavenPublication) {
      artifactId = project.name
      version = gitVersion
      artifact "build/libs/" + archivesBaseName + "-sources.aar"
    }
    javadoc(MavenPublication) {
      artifactId = project.name
      version = gitVersion
      artifact "build/libs/" + archivesBaseName + "-javadoc.jar"
    }
  }
  repositories {
    maven {url = "build/repo"}
  }
}

bintray {
  user = "$bintray_username"
  key = "$bintray_api_key"
  publications = ["library", "sources", "javadoc"]
  pkg {
//    publish = true
    repo = "android"
    userOrg = "moonlitdoor"
    name = group
    licenses = ["MIT"]
    labels = ["Android", "LiveData", "Shared Preferences"]
    websiteUrl = "https://github.com/moonlitdoor/mld-shared-preference-live-data"
    vcsUrl = "https://github.com/moonlitdoor/mld-shared-preference-live-data"
    issueTrackerUrl = "https://github.com/moonlitdoor/mld-shared-preference-live-data/issues"
    githubRepo = "moonlitdoor/mld-shared-preference-live-data"
    version {
      name = gitVersion
      gpg {
        sign = true
      }
//      mavenCentralSync {
//        sync = true //[Default: true] Determines whether to sync the version to Maven Central.
//        user = "userToken" //OSS user token: mandatory
//        password = "paasword" //OSS user password: mandatory
//        close = "1" //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
//      }
    }
  }
}
